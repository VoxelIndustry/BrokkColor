plugins {
    id 'java'
    id "org.sonarqube" version "2.7"
    id "com.jfrog.artifactory" version "4.26.1"
}

apply plugin: 'java-library'
apply plugin: "maven-publish"
apply plugin: "com.jfrog.artifactory"

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven {
        name = 'voxelindustry'
        url = 'https://maven.ferenyr.info/artifactory/libs-release'
    }
}

project.ext.jdkVersion = 8

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(project.jdkVersion))
    }
}

group = "net.voxelindustry"
compileJava.options.encoding = 'UTF-8'
archivesBaseName = "brokkcolor"
version = project.brokkcolor_version

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
    testImplementation 'org.assertj:assertj-core:3.17.2'
}

test {
    useJUnitPlatform()
}

apply plugin: 'org.sonarqube'

sonarqube {
    properties {
        property "sonar.host.url", project.hasProperty('sonarqube_url') ? project.sonarqube_url : System.getenv('SONARQUBE_URL')
        property "sonar.login", project.hasProperty('sonarqube_login') ? project.sonarqube_login : System.getenv('SONARQUBE_LOGIN')
        property "sonar.projectKey", "net.voxelindustry:BrokkColor"
        property "sonar.projectName", "BrokkColor"
        property "sonar.groovy.binaries", "${project.buildDir.name}${File.separator}classes"
    }
}

project.tasks["sonarqube"].dependsOn test

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = 'voxelindustry'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifactId archivesBaseName
            artifact source: sourcesJar, classifier: 'sources', extension: 'jar'
        }
    }
}

artifactoryPublish.dependsOn build